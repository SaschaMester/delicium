# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/module_args/v8.gni")
import("//mojo/public/mojo_application.gni")
import("//testing/test.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

# Repack this here.
repack("unified_blink_resources") {
  sources = [
    "$root_gen_dir/blink/public/resources/blink_image_resources_100_percent.pak",
    "$root_gen_dir/blink/public/resources/blink_resources.pak",
  ]
  output = "$target_gen_dir/unified_blink_resources.pak"
  deps = [
    "//third_party/WebKit/public:image_resources",
    "//third_party/WebKit/public:resources",
  ]
}

action("generate_blink_resource_map") {
  script = "//components/html_viewer/generate_blink_resource_map.py"
  args = [
    "--pak-file",
    rebase_path("$target_gen_dir/unified_blink_resources.pak"),
    "--header",
    rebase_path("$target_gen_dir/blink_resource_map.h"),
    "--cpp",
    rebase_path("$target_gen_dir/blink_resource_map.cc"),
  ]
  outputs = [
    "$target_gen_dir/blink_resource_map.cc",
    "$target_gen_dir/blink_resource_map.h",
  ]
  deps = [
    ":unified_blink_resources",
  ]
  public_deps = [
    "//third_party/WebKit/public:image_resources",
    "//third_party/WebKit/public:resources",
  ]
}

grit("html_viewer_resources_grit") {
  source = "html_viewer_resources.grd"
  outputs = [
    "grit/html_viewer_resources.h",
    "html_viewer_resources.pak",
    "html_viewer_resources.rc",
  ]
}
source_set("lib") {
  sources = [
    "$target_gen_dir/blink_resource_map.cc",
    "$target_gen_dir/blink_resource_map.h",
    "ax_provider_impl.cc",
    "ax_provider_impl.h",
    "blink_basic_type_converters.cc",
    "blink_basic_type_converters.h",
    "blink_input_events_type_converters.cc",
    "blink_input_events_type_converters.h",
    "blink_platform_impl.cc",
    "blink_platform_impl.h",
    "blink_resource_constants.h",
    "blink_url_request_type_converters.cc",
    "blink_url_request_type_converters.h",
    "devtools_agent_impl.cc",
    "devtools_agent_impl.h",
    "discardable_memory_allocator.cc",
    "discardable_memory_allocator.h",
    "document_resource_waiter.cc",
    "document_resource_waiter.h",
    "frame.cc",
    "frame.h",
    "frame_tree_manager.cc",
    "frame_tree_manager.h",
    "frame_tree_manager_delegate.h",
    "global_state.cc",
    "global_state.h",
    "html_document.cc",
    "html_document.h",
    "html_document_oopif.cc",
    "html_document_oopif.h",
    "media_factory.cc",
    "media_factory.h",
    "mock_web_blob_registry_impl.cc",
    "mock_web_blob_registry_impl.h",
    "test_html_viewer_impl.cc",
    "test_html_viewer_impl.h",
    "touch_handler.cc",
    "touch_handler.h",
    "web_clipboard_impl.cc",
    "web_clipboard_impl.h",
    "web_cookie_jar_impl.cc",
    "web_cookie_jar_impl.h",
    "web_layer_tree_view_impl.cc",
    "web_layer_tree_view_impl.h",
    "web_mime_registry_impl.cc",
    "web_mime_registry_impl.h",
    "web_notification_manager_impl.cc",
    "web_notification_manager_impl.h",
    "web_socket_handle_impl.cc",
    "web_socket_handle_impl.h",
    "web_storage_namespace_impl.cc",
    "web_storage_namespace_impl.h",
    "web_theme_engine_impl.cc",
    "web_theme_engine_impl.h",
    "web_url_loader_impl.cc",
    "web_url_loader_impl.h",
  ]

  include_dirs = [ "third_party/WebKit" ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//cc",
    "//cc/blink",
    "//cc/surfaces",
    "//components/clipboard/public/interfaces",
    "//components/devtools_service/public/cpp",
    "//components/devtools_service/public/interfaces",
    "//components/html_viewer/public/interfaces",
    "//components/message_port",
    "//components/mime_util",
    "//components/resource_provider/public/cpp",
    "//components/resource_provider/public/interfaces",
    "//components/scheduler:scheduler",
    "//components/view_manager/gles2:lib",
    "//components/view_manager/public/interfaces",
    "//components/webcrypto",
    "//gin",
    "//mandoline/services/navigation/public/interfaces",
    "//mandoline/tab/public/interfaces",
    "//media",
    "//media/blink",
    "//media/mojo",
    "//mojo/application/public/cpp",
    "//mojo/cc",
    "//mojo/converters/surfaces",
    "//mojo/services/network/public/cpp",
    "//mojo/services/network/public/interfaces",
    "//net",
    "//skia",
    "//third_party/mojo_services/src/accessibility/public/interfaces",
    "//ui/mojo/events:interfaces",
    "//ui/events",
    "//ui/events:gesture_detection",
    "//ui/events/blink",
    "//ui/events/gestures/blink",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/mojo/init",
    "//ui/native_theme",

    # TODO(sky): we shouldn't be using ui_test_pak.
    "//ui/resources:ui_test_pak",
    "//url",
  ]

  public_deps = [
    "//components/view_manager/public/cpp",
    "//third_party/WebKit/public:blink",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    ":generate_blink_resource_map",
  ]

  if (is_win) {
    sources += [ "html_viewer_version.rc" ]
  }

  data_deps = [
    "//components/clipboard",
    "//components/view_manager",
  ]
}

mojo_native_application("html_viewer") {
  sources = [
    "content_handler_impl.cc",
    "content_handler_impl.h",
    "html_document_application_delegate.cc",
    "html_document_application_delegate.h",
    "html_viewer.cc",
    "html_viewer.h",
    "html_viewer_main.cc",
    "html_viewer_switches.cc",
    "html_viewer_switches.h",
  ]
  deps = [
    ":html_viewer_resources_grit",
    ":lib",
    "//mojo/common",
    "//mojo/platform_handle",
    "//third_party/icu:icudata",
    "//third_party/mojo/src/mojo/public/c/system:for_shared_library",
    "//ui/resources:ui_test_pak",
  ]
  data_deps = [ "//mojo/services/network:network" ]
  resources = [
    "$root_out_dir/icudtl.dat",
    "$root_out_dir/ui_test.pak",
    "$target_gen_dir/html_viewer_resources.pak",
  ]

  if (v8_use_external_startup_data) {
    resources += [
      "$root_build_dir/natives_blob.bin",
      "$root_build_dir/snapshot_blob.bin",
    ]
    deps += [ "//gin" ]
  }
}

test("html_viewer_unittests") {
  sources = [
    "ax_provider_impl_unittest.cc",
    "discardable_memory_allocator_unittest.cc",
    "run_all_unittests.cc",
  ]
  deps = [
    ":lib",
    "//base/test:test_support",
    "//third_party/mojo/src/mojo/edk/system",
  ]
}

mojo_native_application("apptests") {
  output_name = "html_viewer_apptests"
  testonly = true

  sources = [
    "ax_provider_apptest.cc",
    "html_frame_apptest.cc",
  ]

  deps = [
    "//base",
    "//components/html_viewer/public/interfaces",
    "//components/view_manager/public/cpp/tests:test_support",
    "//mandoline/tab",
    "//mandoline/tab/public/interfaces",
    "//mojo/application/public/cpp:test_support",
    "//net:test_support",
    "//testing/gtest",
    "//third_party/mojo_services/src/accessibility/public/interfaces",
    "//third_party/mojo/src/mojo/public/c/system:for_shared_library",
  ]

  data_deps = [
    ":html_viewer",
    "//components/view_manager",
  ]
}
